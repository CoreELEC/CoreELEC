KHADAS-UBOOT-CONFIG

#------------------------------------------------------------------------------------------------------
#
# boot.ini
#
# WARNING DO NOT MODIFY THIS FILE! ALL CHANGES WILL BE LOST WITH THE NEXT UPDATE!
# Set your own settings in config.ini
#
#------------------------------------------------------------------------------------------------------

setenv max_freq_a73 "2208"
setenv max_freq_a53 "1800"
setenv hdmi_cec "1"
setenv remotewakeup "0x23dc4db2"
setenv decode_type "0"
setenv remotewakeupmask "0xffffffff"
setenv wol "1"
setenv coreelec "quiet"

setenv rootopt "BOOT_IMAGE=kernel.img boot=UUID=@BOOT_UUID@ disk=UUID=@DISK_UUID@"
setenv consoleopt "console=ttyS0,115200 console=tty0 no_console_suspend"
setenv displayopt "hdmimode=1080p60hz logo=osd0,loaded,0x3d800000"

setenv loadaddr 0x11000000
setenv dtb_mem_addr 0x1000000
setenv uenv_addr 0x13000000

if load mmc ${mmc_dev}:1 ${uenv_addr} config.ini; then env import -t ${uenv_addr} $filesize; fi

if test "${hdmi_cec}" = "1"; then setenv cec "hdmitx=cec3f"; fi
setenv irsetup "remotewakeup=${remotewakeup} decode_type=${decode_type} remotewakeupmask=${remotewakeupmask}"
setenv initargs "${rootopt} ${consoleopt} max_freq_a53=${max_freq_a53} max_freq_a73=${max_freq_a73} wol_enable=${wol} ${cec} ${irsetup}"
setenv bootargs "${initargs} ${displayopt} ${coreelec}"

load mmc ${mmc_dev}:1 ${loadaddr} kernel.img
load mmc ${mmc_dev}:1 ${dtb_mem_addr} dtb.img
fdt addr ${dtb_mem_addr}
booti ${loadaddr} - ${dtb_mem_addr}
bootm
